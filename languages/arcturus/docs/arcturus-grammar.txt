primary-expression          : literal
                            | fully-qualified-name
                            | '('! expression ')'!
                            ;

expression                  : assignement-expression
                            | conditional-expression
                            ;

assignment-expression       : unary-expression '='^ expression
                            ;

conditional-expression      : conditional-or-expression
                            | conditional-or-expression '?'^ expression ':'^ conditional-or-expression
                            ;

conditional-or-expression   : conditional-and-expression
                            | conditional-or-expression '||'^ conditional-and-expression
                            ;

conditional-and-expression  : inclusive-or-expression
                            | conditional-and-expression '&&'^ inclusive-or-expression
                            ;

inclusive-or-expression     : exclusive-or-expression
                            | inclusive-or-expression '|'^ exclusive-or-expression
                            ;

exclusive-or-expression     : and-expression
                            | exclusive-or-expression '^'^ and-expression
                            ;

and-expression              : equality-expression
                            | and-expression '&'^ equality-expression
                            ;

equality-expression         : relational-expression
                            | equality-expression '=='^ relational-expression
                            | equality-expression '!='^ relational-expression
                            ;

relational-expression       : shift-expression
                            | relational-expression '<'^ shift-expression
                            | relational-expression '>'^ shift-expression
                            | relational-expression '<='^ shift-expression
                            | relational-expression '>='^ shift-expression
                            ;

shift-expression            : additive-expression
                            | shift-expression '<<'^ additive-expression
                            | shift-expression '>>'^ additive-expression
                            ;

additive-expression         : multiplicative-expression
                            | additive-expression '+'^ multiplicative-expression
                            | additive-expression '-'^ multiplicative-expression
                            ;

multiplicative-expression   : unary-expression
                            | multiplicative-expression '*'^ unary-expression
                            | multiplicative-expression '/'^ unary-expression
                            ;

unary-expression            : primary-expression
                            | '+'^ unary-expression
                            | '-'^ unary-expression
                            | '!'^ unary-expression
                            | '~'^ unary-expression
                            ;


statement                   : expression-statement
                            | compound-statement
                            | selection-statement
                            ;

expression-statement        : expression-opt
                            ;

expression-opt              : expression
                            | e(psilon)
                            ;

compound-statement          : statements
                            ;

statements                  : statement
                            | statements statement
                            ;


fully-qualified-name        : fully-qualified-name '.' identifier
                            | identifier
                            ;

literal                     : integer-literal
                            | character-literal
                            | floating-literal
                            | string-literal
                            | boolean-literal
                            ;
